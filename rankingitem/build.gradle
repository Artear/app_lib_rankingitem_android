buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath lib.plugin.bintray
        classpath lib.plugin.dcendents
        classpath lib.plugin.dokka
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.github.ben-manes.versions' version '0.21.0'
}

ext {
    bintrayRepo = 'Android'
    bintrayName = 'Ranking-Item'

    publishedGroupId = 'com.artear.stevedore'
    libraryName = 'Ranking-Item'
    artifact = 'rankingitem'

    libraryDescription = 'Rankingitem sample library'

    siteUrl = 'https://github.com/Artear/app_lib_rankingitem_android'
    gitUrl = 'https://github.com/Artear/app_lib_rankingitem_android.git'

    developerId = 'artear'
    developerName = 'Artear Moviles'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

}

static int getVersionCode(branch) {
    def versionCodeCommand = "git rev-list --count " + branch
    return versionCodeCommand.execute().text.trim().toInteger()
}

static def getBranchName() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion

    def branch = getBranchName()
    def vCode = 0 + getVersionCode(branch)

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.getName()}-${defaultConfig.versionName}-${variant.name}.aar"
        }
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode vCode
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Test
    testImplementation lib.junit.library
    androidTestImplementation lib.androidx.testCore
    androidTestImplementation lib.androidx.extJunit
    androidTestImplementation lib.androidx.testRunner
    androidTestImplementation lib.espresso.core

    //Kotlin
    implementation lib.kotlin.standardLib


    //Android X
    implementation lib.androidx.appcompat
    implementation lib.androidx.constraintLayout

    //Artear
    implementation lib.artear.stevedoreItems
    implementation lib.artear.domain
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

group = publishedGroupId
version = libraryVersion

apply from: 'publication.gradle'
apply from: 'bintray.gradle'